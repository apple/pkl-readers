//===----------------------------------------------------------------------===//
// Copyright Â© 2025 Apple Inc. and the Pkl project authors. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//   https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//===----------------------------------------------------------------------===//

module GoReader

extends "Reader.pkl"

import "@gha/actions/Artifact.pkl"
import "@gha/actions/Setup.pkl"
import "@gha/Workflow.pkl"

const runtimeVersion = "1.25"
const gofumptVersion = "0.9.2"
const goJunitReportVersion = "2.1.0"
const goLicensesVersion = "2.0.1"

fixed runtime: "go"

fixed setUpRuntimeStep: Workflow.TypedStep = new Setup.Go {
  with {
    `go-version` = runtimeVersion
    `check-latest` = true
    `cache-dependency-path` = "go.work.sum"
  }
}

fixed readerTestStep: Workflow.Step = new {
  name = "Run Go tests"
  `working-directory` = module.directory
  run =
    // language=bash
    """
    go generate
    git diff --exit-code

    go install github.com/jstemmer/go-junit-report/v2@v\(goJunitReportVersion)
    echo "Running Go unit tests"
    go test -race -v ./... 2>&1 | go-junit-report -iocopy -set-exit-code -out test-results/go-test-results.xml
    """
}

local architectures = List("amd64", "aarch64")

fixed buildJobs: Mapping<String, Workflow.Job> = new {
  ["build-\(module.name)"] {
    `runs-on` = "ubuntu-latest"
    steps {
      for (os in List("macos", "linux")) {
        for (arch in architectures) {
          new {
            `working-directory` = module.directory
            env {
              ["GOOS"] = os
              ["GOARCH"] = arch
            }
            run =
              #"""
              go build \
                -o out/\#(module.cmdName)/\#(module.cmdName)-$GOOS-$GOARCH.bin \
                -ldflags="-X 'main.Version=$(cat pkl/VERSION.txt)'" \
                cmd/\#(module.cmdName)/\#(module.cmdName).go
              """#
          }
        }
      }
      new Artifact.Upload {
        with {
          name = module.buildIntermediatePrefix
          path = "\(module.directory)/out*/\(module.cmdName)/*.bin"
        }
      }
    }
  }
  ["build-\(module.name)-macos-universal-binary"] {
    `runs-on` = new Listing { "macos"; "self-hosted" }
    `if` = "github.repository_owner == 'apple'"
    needs { "build-\(module.name)" }
    steps = new {
      new Artifact.Download {
        with {
          name = module.buildIntermediatePrefix
          path = module.directory
          `merge-multiple` = true
        }
      }
      new {
        `working-directory` = module.directory
        run =
          #"""
          lipo -create \
            -output out/\#(module.cmdName)/\#(module.cmdName)-macos.bin \
            out/\#(module.cmdName)/\#(module.cmdName)-macos-*.bin
          rm out/\#(module.cmdName)/\#(module.cmdName)-macos-*.bin
          """#
      }
      new Artifact.Upload {
        with {
          name = module.buildArtifactPrefix
          path = "\(module.directory)/out*/\(module.cmdName)/*.bin"
        }
      }
    }
  }
}
